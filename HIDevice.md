Proscene provides support for Human Interface Devices (HID) with six (or less) [degrees-of-freedom](http://en.wikipedia.org/wiki/6DOF) to control both the camera and any interactive frame attached to the **scene**. This has been done through the new **HIDevice** class. An **HIDevice** object can be of one of two types: **RELATIVE** or **ABSOLUTE**:

  * A **RELATIVE HIDevice** (default configuration) has a neutral position that the device holds when it is not being manipulated. Examples of such devices are the [3d space navigator](http://en.wikipedia.org/wiki/Space_navigator) and the [joystick](http://en.wikipedia.org/wiki/Joystick).
  * An **ABSOLUTE HIDevice** has no such neutral position. Examples of ABSOLUTE devices are the [wii](http://en.wikipedia.org/wiki/Wii) and the [kinect](http://en.wikipedia.org/wiki/Kinect).

# How to use an [HIDevice](http://en.wikipedia.org/wiki/Human_interface_device)? #

Suppose that we wish to control our camera and some objects in our **scene** using a [6-DOF](http://en.wikipedia.org/wiki/6DOF) device such the [3d space navigator](http://en.wikipedia.org/wiki/Space_navigator). We do it by declaring an **HIDevice** and "feed" it with the output generated by the physical device reported by a third party hardware controller. Once we instantiate an **HIDevice**, say `dev`, we can cycle to the predefined camera and frame configurations calling `dev.nextCameraMode()` and `dev.nextIFrameMode()`, respectively.

To instantiate and define an **HIDevice** we have the following two options (in the code snippets below **note** that the `slider*pos` and the `slider*rot` objects belong to the third party library actually controlling the hardware):

### Option 1: Declare your own HIDevice and add a feed handler ###

```
void setup() {
  ...
  scene = new Scene(this);//declare the scene, which is the main Proscene object
  dev = new HIDevice(scene);//declare a RELATIVE mode HIDevice
  dev.addHandler(this, "feed");//add the feed defined below
  //Define the translation sensitivities
  //(0 will disable the translation, a negative value will reverse its direction)
  dev.setTranslationSensitivity(0.01f, 0.01f, 0.01f);
  //Define the translation sensitivities
  //(0 will disable the rotation, a negative value will reverse its orientation)
  dev.setRotationSensitivity(0.0001f, 0.0001f, 0.0001f);
  scene.addDevice(dev);
}
//The feed handler function should look like the following:
void feed(HIDevice d) {
  //enable the three translations
  //again, 0 will disable the translation, a negative value will reverse its direction
  //to produce a different mapping between the device axes and the different scene coordinates systems' axes
  //(referred to in proscene as Frames) simply exchange the order of the slider*pos parameters at will.
  d.feedTranslation(sliderXpos.getValue(), sliderYpos.getValue(), sliderZpos.getValue());
  //enable the three rotations
  //again, 0 will disable the rotation, a negative value will reverse its orientation
  //to produce a different mapping between the device axes and the different scene frames'
  //axes simply exchange the order of the slider*pos parameters at will.
  d.feedRotation(sliderXrot.getValue(), sliderYrot.getValue(), sliderZrot.getValue());
}
```

### Option 2: Declare your own HIDevice derived class and override the feeds ###

The following code will handle the device exactly as in the previous option.

```
void setup() {
  ...
  scene = new Scene(this);//declare the scene, which is the main Proscene object
  //declare your own HIDevice derived class
  dev = new HIDevice(scene) {
    //if one of the feeds is not overridden the correspondent translation/rotation would be disabled 
    public float feedXTranslation() {
      return sliderXpos.getValue();
    }
    public float feedYTranslation() {
      return sliderYpos.getValue();
    }
    public float feedZTranslation() {
      return sliderZpos.getValue();
    }
    public float feedXRotation() {
      return sliderXrot.getValue();
    }
    public float feedYRotation() {
      return sliderYrot.getValue();
    }
    public float feedZRotation() {
      return sliderZrot.getValue();
    }
  };
  //Define the translation sensitivities
  //(0 will disable the translation, a negative value will reverse its direction)
  dev.setTranslationSensitivity(0.01f, 0.01f, 0.01f);
  //Define the translation sensitivities
  //(0 will disable the rotation, a negative value will reverse its orientation)
  dev.setRotationSensitivity(0.0001f, 0.0001f, 0.0001f);
  scene.addDevice(dev);
}
```

## Observations: ##

  1. **Proscene** does not handle the device directly. This should be achieved  with a third party library such [procontroll](http://www.creativecomputing.cc/p5libs/procontroll/).
  1. An **HIDevice** implements some default configurations to manipulate the camera and a given interactive frame. A third party configuration can easily be implemented too (see the **HIDevice** API for details). **If you implement your own configuration and wish to share it please let us know. If you want us to implement a new one, let us know it too :)**
  1. There is a new example that requires a [3d space navigator](http://en.wikipedia.org/wiki/Space_navigator) which illustrates how to instantiate and use an **HIDevice**: **SpaceNavigator**. The example has been tested in Ubuntu and Windows using **procontroll** but it only works in Windows. This has to do with a **procontroll** issue. Unfortunately no solution seems to arise at the horizon. Testing it in Mac still requires to be done. **Feedback is more than welcome**!
  1. A happy user experience implemented on top of the [TUIO open framework](http://www.tuio.org/) has been reported [here](http://vimeo.com/25224777).
  1. If you implement support for another device **please let us know**. For instance we'd love to see support for the kinect in the near future. We just don't have one to play around at the time ;)