#summary TuioAgent
#sidebar TerseHandlingSideBar

|| Requirements: [https://processing.org/download/ Processing >= 2.x], and the [http://otrolado.info/tersehandling.zip TerseHandling library] (source code found under the "*examples*" section of the library package ||

We are happy to offer this new example in collaboration with [http://edumo.net/wp/ Edumo], whom we are grateful. Running this example requires the following:

|| Extra-requirements: 1. [http://www.tuio.org/?processing TUIO Processing client] and, 2. [https://code.google.com/p/tuiodroid/ tuiodroid] (if you have an Android enable device) and/or a [http://www.tuio.org/?software tuiosimulator] (if you don't)||

<wiki:toc max_depth="1" />

This example shows how to plug-in, and setup a touch device allowing to select and control the circles in our [ActionDrivenCallback  previous example] with it. To make it more interesting, we will allow concurrent finger control, so that various circles can be manipulated with different fingers at the same time. We will also keep the mouse agent intact, so that we still can manipulate the circles using the mouse, as we have done it so far. This examples requires the [http://www.tuio.org/?processing TUIO Processing client] and, either a TUIO tracker such as [https://code.google.com/p/tuiodroid/ android's] (if you have an Android enable device) and/or a [http://www.tuio.org/?software TUIO simulator] (if you don't), to send TUIO messages from.

To achieve this extra-functionality we will:

  # Implement a TUIOAgent.
  # Register the tuioAgent by updating our scene setup.

= Implementing the TUIOAgent =

We declare our TUIOAgent in the same exact way [ActionDrivenCallback#Implementing_a_generic_TerseHandling_agent_using_parameterized_p we've done so far] with our MouseAgent, and then
defining the CHANGE_POSITION motion action binding:

{{{
public class TUIOAgent extends GenericMotionAgent<GenericMotionProfile<MotionAction>, GenericClickProfile<ClickAction>> implements EventConstants {
  ...
  //We use the profile to define the CHANGE_POSITION motion action binding:
  profile().setBinding(TH_LEFT, MotionAction.CHANGE_POSITION);
  ...
}
}}}

and declare a *[*Finger, circle*]* tuple `HashMap` agent extra-attribute, to keep track about which circle is being selected/manipulated by which finger:

{{{
Map<Integer, Grabbable> grabMap = new HashMap<Integer, Grabbable>();
}}}

now, each time a finger starts interaction we invoke `updateGrabber(event)` to check whether or not a circle was picked, and then update the `grabMap` accordingly:

{{{
//this is the TUIO function telling us a new finger enters interaction:
public void addTuioCursor(TuioCursor tcur) {
    //Reduce tuio reported event into its TerseHandling counterpart:
    event = new GenericDOF2Event<MotionAction>(prevEvent, tcur.getScreenX(canvas.width), tcur.getScreenY(canvas.height), 
    0, 0);

    //so we just update the grabber in the agent pool, using our terseEvent:
    updateGrabber(event);
    Grabbable grabbable = this.trackedGrabber();

    //and if something gets catched, we update the hashMap:
    if (grabbable != null) {
      grabMap.put(tcur.getCursorID(), grabbable);
    }
  }
}}}

and, conversely, we also remove the tuple from the `grabMap` when a finger ends interaction as it leaves the tuio touch surface:

{{{
  // called when a cursor is removed from the scene
  public void removeTuioCursor(TuioCursor tcur) {
    event = new GenericDOF2Event<MotionAction>(prevEvent, -1000, -1000, 0, 0);
    // call it for consistency
    updateGrabber(event);
    // remove the tuple
    grabMap.remove(tcur.getCursorID());
  }
}}}

Finally, each time a finger is "dragged", our agent automatically handles the event:

{{{
  public void updateTuioCursor(TuioCursor tcur) {
    //retrieve the circle according to the given finger:
    Grabbable trackedGrabber = grabMap.get(tcur.getCursorID());
    if (trackedGrabber != null) {
      //reduce the tuio event into its TerseEvent counterpart:
      event = new GenericDOF2Event<MotionAction>(prevEvent, 
      tcur.getScreenX(canvas.width), 
      tcur.getScreenY(canvas.height), 0, TH_LEFT);
      //disable tracking to not intervene with the action being performed,
      //possibly, by more than one finger:
      disableTracking();
      setDefaultGrabber(trackedGrabber);
      handle(event);
      //renable tracking to allow more fingers to be added or removed:
      enableTracking();
    } 
    else {
    }
    prevEvent = event.get();
  }
}}}

= Scene setup update =

Now that we have implemented our TUIOAgent, we just add it to our TerseHandler, using the scene [http://processing.org/reference/setup_.html setup], and fill in with our circles:

{{{
...
MOUSEAgent mouseAgent;
TUIOAgent tuioAgent;
...
void setup() {
  terseHandler = new TerseHandler();
  //instantiate the new agent
  tuioAgent = new TUIOAgent(terseHandler, "my_tuio", g);
  mouseAgent = new MOUSEAgent(terseHandler, "my_mouse");
  registerMethod("mouseEvent", mouseAgent);
  circles = new GrabbableCircle[50];
  for (int i = 0; i < circles.length; i++) {
    //circles are added to the mouse agent by default
    circles[i] = new GrabbableCircle(this, g, mouseAgent);
    //we also add them to the tuio's:
    tuioAgent.addInPool(circles[i]);
  }
  ...
}
}}}

Now you can drag the circles around using your fingers with a touch device.